// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Callbacks for RiRuntime to do x86-64-linux logic for handling signals and walking
// stack frames.
component RiOs {
	private def sigactionbuf: Array<long> = [
		0,		// sa_handler
		0x04000000,	// sa_flags
		0,		// sa_restorer
		0		// sa_mask
	];
	// Install the {CiRuntime.signalStub}, which calls an RiRuntime routine, for {signum}.
	def installHandler(signum: int) {
		sigactionbuf[0] = CiRuntime.signalStub - Pointer.NULL;
		sigactionbuf[1] = 0x0C000000;
		sigactionbuf[2] = CiRuntime.signalRestorer - Pointer.NULL;
		Linux.syscall(LinuxConst.SYS_rt_sigaction, (signum, Pointer.atContents(sigactionbuf), 0, 8));
	}
	// Get the instruction pointer from a ucontext.
	def getIp(ucontext: Pointer) -> Pointer {
		return (ucontext + 168).load<Pointer>();
	}
	// Get the stack pointer from a ucontext.
	def getSp(ucontext: Pointer) -> Pointer {
		return (ucontext + 160).load<Pointer>();
	}
	// Get the frame pointer from a ucontext.
	def getFp(ucontext: Pointer) -> Pointer {
		return (ucontext + 120).load<Pointer>();
	}
	// Get the access address from a ucontext.
	def getAccessAddress(siginfo: Pointer, ucontext: Pointer) -> Pointer {
		return (ucontext + 216).load<Pointer>();
	}
	// Advance the instruction pointer and stack pointer from the current frame to the caller's frame.
	def callerFrame(ip: Pointer, sp: Pointer, frameWords: int) -> (Pointer, Pointer) {
		sp = sp + (1 + frameWords) * Pointer.SIZE;
		ip = (sp + (0 - Pointer.SIZE)).load<Pointer>() + -1;
		return (ip, sp);
	}
	// Exit with the given return code.
	def exit(code: int) {
	        Linux.syscall(LinuxConst.SYS_exit, code);
	}
	// initialize stack red zone for handling stack overflow
	def initStackRedZone(start: Pointer, end: Pointer) -> (Pointer, Pointer) {
		var delta = end - start;
		def PAGE_SIZE = 4 * 1024;
		if (delta < (3 * PAGE_SIZE)) return (Pointer.NULL, Pointer.NULL); // not enough space for red zone
		// | alt stack | red zone |     usable stack   |
		// ^ start                                     ^end
		def ALT_SIZE = PAGE_SIZE;
		var red_start = start + ALT_SIZE;
		var red_end = red_start + PAGE_SIZE;
		// mprotect a page PROT_NONE to serve as red zone for stack overflow
		Linux.syscall(LinuxConst.SYS_mprotect, (red_start, PAGE_SIZE, LinuxConst.PROT_NONE));

		// set up alternate signal stack (using pages below red zone)
		sigactionbuf[0] = start - Pointer.NULL;	// start
		sigactionbuf[1] = 0;			// flags
		sigactionbuf[2] = ALT_SIZE;		// size
		Linux.syscall(LinuxConst.SYS_sigaltstack, (Pointer.atContents(sigactionbuf), Pointer.NULL));
		return (red_start, red_end);
	}
}
