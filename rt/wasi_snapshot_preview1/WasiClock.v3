// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Scales a clock from an underlying resolution to a higher or lower resolution.
class WasiClock(ns: u64) {
	var id = -1;
	private var mul = 1uL;
	private var div = 1uL;

	// initialize (or improve) the scaling of an underlying clock with {res}
	def init(clock: int, res: u64) {
		if (res == ns) { // exact match
			id = clock;
			mul = div = 1;
			return;
		} else if (id < 0 || res < ns) { // if none yet, or better resolution
			id = clock;
			mul = res; // convert systicks to ns
			div = ns;  // convert ns to clock ticks
		}
		if ((mul % div) == 0) { // optimize exact upscale
			mul /= div;
			div = 1;
		}
		if ((div % mul) == 0) { // optimize exact downscale
			div /= mul;
			mul = 1;
		}
	}
	// scale a value of underlying ticks {val} to this resolution
	def scale(val: u64) -> u64 {
		if (mul > 1) val *= mul;
		if (div > 1) val /= div;
		return val;
	}
}