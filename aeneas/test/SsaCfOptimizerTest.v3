def TEST = UnitTest.new("SsaCfOptimizer", runAllTests);
var prog: Program;
var context: SsaContext;
def H: SsaGraphBuildingHelper;

def runAllTests() {
	prog = Program.new();
	context = SsaContext.new(null, prog);

	doRemoveEdge();
	doKillInstr();
	doKillBlock();
	doKillSuccs();
	doOptEdge();
	doReplaceWithGoto();
	doSimplifyPhis();
	doBranchSensitive();
	doDiamondDiamond();

	prog = null;
	context = null;
}

def printGraph(phase: string, g: SsaGraph) {
	var p = SsaPrinter.new();
	p.printGraph(phase, StringBuilder.puts(_, "TestGraph"), g, null);
	p.flush();
}

def optGraph(g: SsaGraph) {
	context.graph = g;
	var cf = SsaCfOptimizer.new(context);
	cf.optimize();
}

def INT32_ADD = binop(Int.TYPE.opAdd(), _, _);

def binop(op: Operator, x: SsaInstr, y: SsaInstr) -> SsaInstr {
	var i = SsaApplyOp.new(null, op, [x, y]);
	i.setFact(Opcodes.facts(op.opcode));
	return i;
}

def doRemoveEdge() {
	var g = SsaGraph.new([], Int.TYPE);
	context.graph = g;
	var cf = SsaCfOptimizer.new(context);
	def A = SsaInstrMatchHelper.new(prog, TEST, null);
	for (rm < 4) {
		// Create 4 blocks that feed into a common destination
		var blocks = [SsaBlock.new(), SsaBlock.new(), SsaBlock.new(), SsaBlock.new()];
		var vals: Array<SsaInstr> = [g.intConst(3), g.intConst(4), g.intConst(5), g.intConst(6)];
		var d = SsaBlock.new();
		for (b in blocks) b.append(SsaGoto.new(d));
		for (b in blocks) A.assertSuccs(b, [d]);
		A.assertPreds(blocks, d);

		// Create a phi with 4 inputs
		var phi = SsaPhi.new(Int.TYPE, d, vals);
		d.append(phi);

		// Remove one of the edges
		var rem = blocks[rm];
		var sblocks = Arrays.deleteAt(blocks, rm);
		var svals = Arrays.deleteAt(vals, rm);
		
		cf.removeEdge(rem.succ(0));
		for (b in sblocks) A.assertSuccs(b, [d]);
		A.assertPreds(sblocks, d);
		A.assertInputs(phi, svals);
	}

	// Check simplification of phis
	for (rm < 2) {
		// Create 2 blocks that feed into a common destination
		var blocks = [SsaBlock.new(), SsaBlock.new()];
		var vals: Array<SsaInstr> = [g.intConst(4), g.intConst(5)];
		var d = SsaBlock.new();
		for (b in blocks) b.append(SsaGoto.new(d));
		for (b in blocks) A.assertSuccs(b, [d]);
		A.assertPreds(blocks, d);

		// Create a phi with 4 inputs
		var phi = SsaPhi.new(Int.TYPE, d, vals);
		d.append(phi);
		var ret = SsaReturn.new([phi]);
		d.append(ret);

		// Remove one of the edges
		var rem = blocks[rm];
		var sblocks = Arrays.deleteAt(blocks, rm);
		var svals = Arrays.deleteAt(vals, rm);
		
		cf.removeEdge(rem.succ(0));
		for (b in sblocks) A.assertSuccs(b, [d]);
		A.assertPreds(sblocks, d);

		// Check the phi has been simplified and removed
		A.assertInputs(ret, svals);
		TEST.eq(ret, d.next);
		TEST.eq(d, ret.prev);
		TEST.eq(true, phi.facts.O_KILLED);
	}
}

def doKillInstr() {
	var p0 = SsaParam.new(0, Int.TYPE);
	var p1 = SsaParam.new(1, Int.TYPE);
	var g = SsaGraph.new([p0, p1], Int.TYPE);
	context.graph = g;

	var cf = SsaCfOptimizer.new(context);
	def A = SsaInstrMatchHelper.new(prog, TEST, null);

	// Simple test.
	var block = SsaBlock.new();
	var add = H.INT32_ADD(p0, p1);
	block.append(add);
	A.assertBlock(block, [add]);
	cf.killInstr(add);
	A.assertBlock(block, []);
	A.assertKilled(add);
	A.assertNoUse(add, p0);
	A.assertNoUse(add, p1);

	// Test with instruction in the middle.
	block = SsaBlock.new();
	var i0 = H.INT32_ADD(p0, p1);
	var i1 = H.INT32_MUL(i0, i0);
	var i2 = H.INT32_AND(p0, p1);
	block.append(i0);
	block.append(i1);
	block.append(i2);
	cf.killInstr(i1);
	A.assertBlock(block, [i0, i2]);
	A.assertKilled(i1);
	A.assertNoUse(i1, i0);
}

def doKillBlock() {
	var p0 = SsaParam.new(0, Int.TYPE);
	var p1 = SsaParam.new(1, Int.TYPE);
	var g = SsaGraph.new([p0, p1], Int.TYPE);
	context.graph = g;

	var cf = SsaCfOptimizer.new(context);
	def A = SsaInstrMatchHelper.new(prog, TEST, null);

	// Test killing an entire block.
	var block = SsaBlock.new();
	var i0: SsaInstr;
	var instrs = [
		i0 = H.INT32_ADD(p0, p1),
		H.INT32_MUL(i0, i0),
		H.INT32_AND(p0, p1)
	];

	for (i in instrs) block.append(i);

	cf.killBlock(block);

	A.assertBlock(block, []);
	for (i in instrs) {
		A.assertKilled(i);
		A.assertNoUse(i, p0);
	}
}

def doKillSuccs() { } // TODO
def doOptEdge() { } // TODO
def doReplaceWithGoto() { } // TODO
def doSimplifyPhis() { } // TODO
def doBranchSensitive() { } // TODO

def doDiamondDiamond() {
	var p0 = SsaParam.new(0, Int.TYPE);
	var g = SsaGraph.new([p0], Int.TYPE);
	def A = SsaInstrMatchHelper.new(prog, TEST, null);

	var top = g.startBlock;
	var d1 = Diamond.new(top, p0);
	var phi = d1.phi(Bool.TYPE, g.trueConst(), g.falseConst());
	var d2 = Diamond.new(d1.merge, phi);
	var m = d2.phi(Int.TYPE, g.oneConst(), g.twoConst());
	var ret = SsaReturn.new([m]);
	d2.merge.append(ret);

	optGraph(g);
	// TODO: clean up intermediate gotos and assert
	A.assertBlock(d2.merge, [m, ret]);
}
